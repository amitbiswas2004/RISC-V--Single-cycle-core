`timescale 1ns/1ps

module register_file #(
    parameter XLEN = 32,
    parameter REG_COUNT = 32
) (
    input  wire                      clk,
    input  wire                      we,
    input  wire [4:0]                rs1_addr,
    input  wire [4:0]                rs2_addr,
    input  wire [4:0]                rd_addr,
    input  wire [XLEN-1:0]           rd_data,
    output wire [XLEN-1:0]           rs1_data,
    output wire [XLEN-1:0]           rs2_data
);
    reg [XLEN-1:0] regs [0:REG_COUNT-1];

    integer i;
    initial begin
        for (i = 0; i < REG_COUNT; i = i + 1)
            regs[i] = {XLEN{1'b0}};
    end

    always @(posedge clk) begin
        if (we && (rd_addr != 5'd0))
            regs[rd_addr] <= rd_data;
    end

    assign rs1_data = (rs1_addr == 5'd0) ? {XLEN{1'b0}} : regs[rs1_addr];
    assign rs2_data = (rs2_addr == 5'd0) ? {XLEN{1'b0}} : regs[rs2_addr];
endmodule
